apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "com.squareup.sqldelight"

def javaVersion = JavaVersion.VERSION_1_8
def appPackageName = "com.raywenderlich.android.sqldelight"

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    applicationId appPackageName
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    coreLibraryDesugaringEnabled = true
  }

  kotlinOptions {
    jvmTarget = javaVersion.toString()
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
    }
  }

  buildFeatures {
    viewBinding true
  }
}

sqldelight {
  Database {
    packageName = "${appPackageName}.db"

    sourceFolders = ["sqldelight"]

    schemaOutputDirectory = file("src/main/sqldelight/schemas")
    verifyMigrations = true
  }
}

dependencies {
  coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

  // Kotlin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

  // Support Libraries
  implementation "androidx.appcompat:appcompat:1.2.0"
  implementation "androidx.cardview:cardview:1.0.0"
  implementation "androidx.fragment:fragment-ktx:1.3.2"
  implementation "androidx.constraintlayout:constraintlayout:2.0.4"
  implementation "com.google.android.material:material:1.3.0"

  // SQLDelight
  implementation "com.squareup.sqldelight:android-driver:$sqldelightVersion"
  implementation "com.squareup.sqldelight:coroutines-extensions-jvm:$sqldelightVersion"

  // Kotlin Coroutines
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3"

  // Image Loading
  implementation "io.coil-kt:coil:1.1.1"

  // Testing
  testImplementation "junit:junit:4.13.2"
  testImplementation "com.squareup.sqldelight:sqlite-driver:$sqldelightVersion"
}
